terraform {

  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
}

resource "yandex_vpc_security_group" "main" {
  name        = "${var.env}-security group for ${var.projectname}"
  description = ""
  network_id  = var.network

#================== Simple rules ======================  

  dynamic "ingress" {
    for_each = var.allow_ingress_ports_tcp
    content {
      protocol       = "TCP"
      description    = "Rule generated by dynamic terraform (simple mode)"
      v4_cidr_blocks = ["0.0.0.0/0"]
      port           = ingress.value
    }
  }

  dynamic "egress" {
    for_each = var.allow_egress_ports_tcp
    content {
      protocol       = "ANY"
      description    = "Rule generated by dynamic terraform (simple mode)"
      v4_cidr_blocks = ["0.0.0.0/0"]
      port           = egress.value
    }
  }

#================= Advanced rules ====================

  dynamic "ingress" {
    for_each = var.ingress_rules_advanced
    content {
      protocol       = ingress.value.protocol
      description    = ingress.value.description
      v4_cidr_blocks = ingress.value.v4_cidr_blocks
      port           = ingress.value.port
    }
  }

  dynamic "egress" {
    for_each = var.egress_rules_advanced
    content {
      protocol       = egress.value.protocol
      description    = egress.value.description
      v4_cidr_blocks = egress.value.v4_cidr_blocks
      port           = egress.value.port
    }
  }
}
